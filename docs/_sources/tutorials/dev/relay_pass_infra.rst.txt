.. note::
    :class: sphx-glr-download-link-note

    Click :ref:`here <sphx_glr_download_tutorials_dev_relay_pass_infra.py>` to download the full example code
.. rst-class:: sphx-glr-example-title

.. _sphx_glr_tutorials_dev_relay_pass_infra.py:


.. _tutorial-relay-pass-infra:

How to Use Relay Pass Infra
===========================
**Author**: `Zhi Chen <https://github.com/zhiics>`_

As the number of optimization passes increases in Relay, it becomes intractable to
execute them and maintain their dependencies manually. Therefore, we have
introduced an infrastructure to manage the optimization passes.

The optimizations of a Relay program could be applied at various granularity,
namely function-level and module-level using :py:class:`tvm.relay.transform.FunctionPass`
and :py:class:`tvm.relay.transform.ModulePass`
respectively. Or users can rely on py:class:`tvm.transform.Sequential` to apply a sequence of passes
on a Relay program where the dependencies between passes can be resolved by the
pass infra. For more details about each type of these passes, please refer to
the :ref:`relay-pass-infra`

This tutorial demostrates how developers can use the Relay pass infra to perform
a certain optimization and create an optimization pipeline.


.. code-block:: default


    import numpy as np
    import tvm
    from tvm import te
    import tvm.relay as relay







Create An Example Relay Program
-------------------------------
First of all, we create a simple Relay program for the tutorial. This program
will be used by various optimizations of the examples in this tutorial.


.. code-block:: default


    def example():
        shape = (1, 64, 54, 54)
        c_data = np.empty(shape).astype("float32")
        c = relay.const(c_data)
        weight = relay.var('weight', shape=(64, 64, 3, 3))
        x = relay.var("x", relay.TensorType((1, 64, 56, 56), "float32"))
        conv = relay.nn.conv2d(x, weight)
        y = relay.add(c, c)
        y = relay.multiply(y, relay.const(2, "float32"))
        y = relay.add(conv, y)
        z = relay.add(y, c)
        z1 = relay.add(y, c)
        z2 = relay.add(z, z1)
        return relay.Function([x], z2)







Let us register layout alteration for a conv2d op so that we can apply the
layout alteration pass on the example. How alter layout pass works is out
the scope of this tutorial.


.. code-block:: default


    @relay.op.register_alter_op_layout("nn.conv2d", level=101)
    def alter_conv2d(attrs, inputs, tinfos, out_type):
        data, weight = inputs
        new_attrs = dict(attrs)
        new_attrs['data_layout'] = 'NCHW16c'
        return relay.nn.conv2d(data, weight, **new_attrs)







Optimize the Program
--------------------
Now we would like to optimize the program. Relay features a host of
optimizations. We will select some of them to apply on this example program.

There are multiple ways to optimize a Relay program. Below we will provide
examples for each of them.

Manually Apply Optimization Passes
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~


.. code-block:: default


    # Let's first create a relay Module which contains one or multiple Relay
    # functions for optimization.
    f = example()
    mod = tvm.IRModule.from_expr(f)

    # Now we can apply constant folding on the module.
    # fold_const here is a callback that doesn't take any parameters.
    fold_const = relay.transform.FoldConstant()
    # Then, we can invoke the pass on the given module. Note that the constant
    # folding pass works at the function-level. That being said, each function in
    # the module will be applied with the optimization. Users don't need to iterate
    # through individual functions manually to apply this pass.
    mod = fold_const(mod)
    # We can see from the updated program that the constants are folded.
    print(mod)





.. rst-class:: sphx-glr-script-out

 Out:

 .. code-block:: none

    def @main(%x: Tensor[(1, 64, 56, 56), float32], %weight: Tensor[(64, 64, 3, 3), float32]) -> Tensor[(1, 64, 54, 54), float32] {
      %0 = nn.conv2d(%x, %weight, padding=[0, 0, 0, 0]) /* ty=Tensor[(1, 64, 54, 54), float32] */;
      %1 = add(%0, meta[relay.Constant][0] /* ty=Tensor[(1, 64, 54, 54), float32] */ /* ty=Tensor[(1, 64, 54, 54), float32] */) /* ty=Tensor[(1, 64, 54, 54), float32] */;
      %2 = add(%1, meta[relay.Constant][1] /* ty=Tensor[(1, 64, 54, 54), float32] */ /* ty=Tensor[(1, 64, 54, 54), float32] */) /* ty=Tensor[(1, 64, 54, 54), float32] */;
      %3 = add(%1, meta[relay.Constant][1] /* ty=Tensor[(1, 64, 54, 54), float32] */ /* ty=Tensor[(1, 64, 54, 54), float32] */) /* ty=Tensor[(1, 64, 54, 54), float32] */;
      add(%2, %3) /* ty=Tensor[(1, 64, 54, 54), float32] */
    }

    // meta data omitted. you can use show_meta_data=True to include meta data



More optimizations can be applied in the similar manner. For instance, we can
eliminate the common expressions that used by `z` and `z1`.


.. code-block:: default

    mod = relay.transform.EliminateCommonSubexpr()(mod)
    print(mod)





.. rst-class:: sphx-glr-script-out

 Out:

 .. code-block:: none

    def @main(%x: Tensor[(1, 64, 56, 56), float32], %weight: Tensor[(64, 64, 3, 3), float32]) -> Tensor[(1, 64, 54, 54), float32] {
      %0 = nn.conv2d(%x, %weight, padding=[0, 0, 0, 0]) /* ty=Tensor[(1, 64, 54, 54), float32] */;
      %1 = add(%0, meta[relay.Constant][0] /* ty=Tensor[(1, 64, 54, 54), float32] */ /* ty=Tensor[(1, 64, 54, 54), float32] */) /* ty=Tensor[(1, 64, 54, 54), float32] */;
      %2 = add(%1, meta[relay.Constant][1] /* ty=Tensor[(1, 64, 54, 54), float32] */ /* ty=Tensor[(1, 64, 54, 54), float32] */) /* ty=Tensor[(1, 64, 54, 54), float32] */;
      add(%2, %2) /* ty=Tensor[(1, 64, 54, 54), float32] */
    }

    // meta data omitted. you can use show_meta_data=True to include meta data



Some optimizations, such as fusion, are parameteric as well. For example,
opt level 0 will not allow operators to be fused together. Users can pass the
`fuse_opt_level` to enable this.


.. code-block:: default

    mod = relay.transform.FuseOps(fuse_opt_level=0)(mod)

    # We can observe that the optimized module contains functions that only have
    # a signle primitive op.
    print(mod)





.. rst-class:: sphx-glr-script-out

 Out:

 .. code-block:: none

    def @main(%x: Tensor[(1, 64, 56, 56), float32], %weight: Tensor[(64, 64, 3, 3), float32]) -> Tensor[(1, 64, 54, 54), float32] {
      %0 = fn (%p0: Tensor[(1, 64, 56, 56), float32], %p1: Tensor[(64, 64, 3, 3), float32], Primitive=1) -> Tensor[(1, 64, 54, 54), float32] {
        nn.conv2d(%p0, %p1, padding=[0, 0, 0, 0]) /* ty=Tensor[(1, 64, 54, 54), float32] */
      };
      %1 = %0(%x, %weight) /* ty=Tensor[(1, 64, 54, 54), float32] */;
      %2 = fn (%p01: Tensor[(1, 64, 54, 54), float32], %p11: Tensor[(1, 64, 54, 54), float32], Primitive=1) -> Tensor[(1, 64, 54, 54), float32] {
        add(%p01, %p11) /* ty=Tensor[(1, 64, 54, 54), float32] */
      };
      %3 = %2(%1, meta[relay.Constant][0] /* ty=Tensor[(1, 64, 54, 54), float32] */ /* ty=Tensor[(1, 64, 54, 54), float32] */) /* ty=Tensor[(1, 64, 54, 54), float32] */;
      %4 = fn (%p02: Tensor[(1, 64, 54, 54), float32], %p12: Tensor[(1, 64, 54, 54), float32], Primitive=1) -> Tensor[(1, 64, 54, 54), float32] {
        add(%p02, %p12) /* ty=Tensor[(1, 64, 54, 54), float32] */
      };
      %5 = %4(%3, meta[relay.Constant][1] /* ty=Tensor[(1, 64, 54, 54), float32] */ /* ty=Tensor[(1, 64, 54, 54), float32] */) /* ty=Tensor[(1, 64, 54, 54), float32] */;
      %6 = fn (%p03: Tensor[(1, 64, 54, 54), float32], Primitive=1) -> Tensor[(1, 64, 54, 54), float32] {
        add(%p03, %p03) /* ty=Tensor[(1, 64, 54, 54), float32] */
      };
      %6(%5) /* ty=Tensor[(1, 64, 54, 54), float32] */
    }

    // meta data omitted. you can use show_meta_data=True to include meta data



Use Sequential to Apply a Sequence of Passes
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
Applying passes as above is actually tedious and it may require users to have
better understanding about the dependencies between them. For example, fusion
currently doesn't work well on let bindings. Therefore, we would not be able
to fuse operators that were fusable if :py:func:`relay.transform.ToANormalForm` is applied before
fusion, as this pass generates let bindings for each expression to
canonicalize a Relay program.

Relay, hence, provides :py:class:`tvm.transform.Sequential` to alleviate developers from handling
these issues explicitly by specifying the required passes of each pass and
packing them as a whole to execute. For example, the same passes can now be
applied using the sequential style as the following. :py:class:`tvm.transform.Sequential` is
similiar to `torch.nn.sequential <https://pytorch.org/docs/stable/nn.html#torch.nn.Sequential>`_
and `mxnet.gluon.block <https://mxnet.incubator.apache.org/api/python/docs/_modules/mxnet/gluon/block.html>`_.
For example, `torch.nn.sequential` is used to contain a sequence of PyTorch
`Modules` that will be added to build a network. It focuses on the network
layers. Instead, the :py:class:`tvm.transform.Sequential` in our pass infra works on the optimizing
pass.


.. code-block:: default


    # Now let's execute some passes through :py:class:`tvm.transform.Sequential`
    f = example()
    mod = tvm.IRModule.from_expr(f)
    # Glob the interested passes.
    seq = tvm.transform.Sequential([relay.transform.FoldConstant(),
                                      relay.transform.EliminateCommonSubexpr(),
                                      relay.transform.FuseOps(fuse_opt_level=2)])
    mod1 = seq(mod)
    print(mod1)





.. rst-class:: sphx-glr-script-out

 Out:

 .. code-block:: none

    def @main(%x: Tensor[(1, 64, 56, 56), float32], %weight: Tensor[(64, 64, 3, 3), float32]) -> Tensor[(1, 64, 54, 54), float32] {
      %4 = fn (%p0: Tensor[(1, 64, 56, 56), float32], %p1: Tensor[(64, 64, 3, 3), float32], %p2: Tensor[(1, 64, 54, 54), float32], %p3: Tensor[(1, 64, 54, 54), float32], Primitive=1) -> Tensor[(1, 64, 54, 54), float32] {
        %0 = nn.conv2d(%p0, %p1, padding=[0, 0, 0, 0]) /* ty=Tensor[(1, 64, 54, 54), float32] */;
        %1 = add(%0, %p2) /* ty=Tensor[(1, 64, 54, 54), float32] */;
        %2 = add(%1, %p3) /* ty=Tensor[(1, 64, 54, 54), float32] */;
        %3 = add(%1, %p3) /* ty=Tensor[(1, 64, 54, 54), float32] */;
        add(%2, %3) /* ty=Tensor[(1, 64, 54, 54), float32] */
      };
      %4(%x, %weight, meta[relay.Constant][0] /* ty=Tensor[(1, 64, 54, 54), float32] */ /* ty=Tensor[(1, 64, 54, 54), float32] */, meta[relay.Constant][1] /* ty=Tensor[(1, 64, 54, 54), float32] */ /* ty=Tensor[(1, 64, 54, 54), float32] */) /* ty=Tensor[(1, 64, 54, 54), float32] */
    }

    // meta data omitted. you can use show_meta_data=True to include meta data



From the transformed Relay program, we can see that there are still two
identical addition operations. This is because `EliminateCommonSubexpr`
was not actually performed. The reason is because only the passes that have
optimization level less or equal to 2 will be executed by default under
:py:class:`tvm.transform.Sequential`. The pass infra,
however, provides a configuration interface
for users to customize the optimization level that they want to execute.


.. code-block:: default


    with tvm.transform.PassContext(opt_level=3):
        mod2 = seq(mod)
    print(mod2)





.. rst-class:: sphx-glr-script-out

 Out:

 .. code-block:: none

    def @main(%x: Tensor[(1, 64, 56, 56), float32], %weight: Tensor[(64, 64, 3, 3), float32]) -> Tensor[(1, 64, 54, 54), float32] {
      %3 = fn (%p0: Tensor[(1, 64, 56, 56), float32], %p1: Tensor[(64, 64, 3, 3), float32], %p2: Tensor[(1, 64, 54, 54), float32], %p3: Tensor[(1, 64, 54, 54), float32], Primitive=1) -> Tensor[(1, 64, 54, 54), float32] {
        %0 = nn.conv2d(%p0, %p1, padding=[0, 0, 0, 0]) /* ty=Tensor[(1, 64, 54, 54), float32] */;
        %1 = add(%0, %p2) /* ty=Tensor[(1, 64, 54, 54), float32] */;
        %2 = add(%1, %p3) /* ty=Tensor[(1, 64, 54, 54), float32] */;
        add(%2, %2) /* ty=Tensor[(1, 64, 54, 54), float32] */
      };
      %3(%x, %weight, meta[relay.Constant][0] /* ty=Tensor[(1, 64, 54, 54), float32] */ /* ty=Tensor[(1, 64, 54, 54), float32] */, meta[relay.Constant][1] /* ty=Tensor[(1, 64, 54, 54), float32] */ /* ty=Tensor[(1, 64, 54, 54), float32] */) /* ty=Tensor[(1, 64, 54, 54), float32] */
    }

    // meta data omitted. you can use show_meta_data=True to include meta data



Now we can see that only one of the two identical additions is kept.

In addition, users can selectively disable some passes using the
`disabled_pass` config, which is similar to the `-fno-xxx` option used the
general purpose compilers, such as Clang and GCC. For example, we can disable
EliminateCommonSubexpr as following. The printed module will again show two
identical addition operations.


.. code-block:: default


    with tvm.transform.PassContext(opt_level=3, disabled_pass=["EliminateCommonSubexpr"]):
        mod3 = seq(mod)
    print(mod3)





.. rst-class:: sphx-glr-script-out

 Out:

 .. code-block:: none

    def @main(%x: Tensor[(1, 64, 56, 56), float32], %weight: Tensor[(64, 64, 3, 3), float32]) -> Tensor[(1, 64, 54, 54), float32] {
      %4 = fn (%p0: Tensor[(1, 64, 56, 56), float32], %p1: Tensor[(64, 64, 3, 3), float32], %p2: Tensor[(1, 64, 54, 54), float32], %p3: Tensor[(1, 64, 54, 54), float32], Primitive=1) -> Tensor[(1, 64, 54, 54), float32] {
        %0 = nn.conv2d(%p0, %p1, padding=[0, 0, 0, 0]) /* ty=Tensor[(1, 64, 54, 54), float32] */;
        %1 = add(%0, %p2) /* ty=Tensor[(1, 64, 54, 54), float32] */;
        %2 = add(%1, %p3) /* ty=Tensor[(1, 64, 54, 54), float32] */;
        %3 = add(%1, %p3) /* ty=Tensor[(1, 64, 54, 54), float32] */;
        add(%2, %3) /* ty=Tensor[(1, 64, 54, 54), float32] */
      };
      %4(%x, %weight, meta[relay.Constant][0] /* ty=Tensor[(1, 64, 54, 54), float32] */ /* ty=Tensor[(1, 64, 54, 54), float32] */, meta[relay.Constant][1] /* ty=Tensor[(1, 64, 54, 54), float32] */ /* ty=Tensor[(1, 64, 54, 54), float32] */) /* ty=Tensor[(1, 64, 54, 54), float32] */
    }

    // meta data omitted. you can use show_meta_data=True to include meta data



The passes applied so far are target independent. The pass infra also
provides a means to make pass target-aware. For example, the layout
alteration pass falls in such category.


.. code-block:: default


    with tvm.transform.PassContext(opt_level=3):
        mod4 = seq(mod)
    print(mod4)

    seq1 = tvm.transform.Sequential([relay.transform.AlterOpLayout()])
    with tvm.transform.PassContext(opt_level=3):
        with tvm.target.create("llvm"):
            mod5 = seq1(mod)
    print(mod5)





.. rst-class:: sphx-glr-script-out

 Out:

 .. code-block:: none

    def @main(%x: Tensor[(1, 64, 56, 56), float32], %weight: Tensor[(64, 64, 3, 3), float32]) -> Tensor[(1, 64, 54, 54), float32] {
      %3 = fn (%p0: Tensor[(1, 64, 56, 56), float32], %p1: Tensor[(64, 64, 3, 3), float32], %p2: Tensor[(1, 64, 54, 54), float32], %p3: Tensor[(1, 64, 54, 54), float32], Primitive=1) -> Tensor[(1, 64, 54, 54), float32] {
        %0 = nn.conv2d(%p0, %p1, padding=[0, 0, 0, 0]) /* ty=Tensor[(1, 64, 54, 54), float32] */;
        %1 = add(%0, %p2) /* ty=Tensor[(1, 64, 54, 54), float32] */;
        %2 = add(%1, %p3) /* ty=Tensor[(1, 64, 54, 54), float32] */;
        add(%2, %2) /* ty=Tensor[(1, 64, 54, 54), float32] */
      };
      %3(%x, %weight, meta[relay.Constant][0] /* ty=Tensor[(1, 64, 54, 54), float32] */ /* ty=Tensor[(1, 64, 54, 54), float32] */, meta[relay.Constant][1] /* ty=Tensor[(1, 64, 54, 54), float32] */ /* ty=Tensor[(1, 64, 54, 54), float32] */) /* ty=Tensor[(1, 64, 54, 54), float32] */
    }

    // meta data omitted. you can use show_meta_data=True to include meta data
    def @main(%x: Tensor[(1, 64, 56, 56), float32], %weight: Tensor[(64, 64, 3, 3), float32]) -> Tensor[(1, 64, 54, 54), float32] {
      %0 = layout_transform(%x, src_layout="NCHW", dst_layout="NCHW16c") /* ty=Tensor[(1, 4, 56, 56, 16), float32] */;
      %1 = nn.conv2d(%0, %weight, padding=[0, 0, 0, 0], data_layout="NCHW16c") /* ty=Tensor[(1, 4, 54, 54, 16), float32] */;
      %2 = add(meta[relay.Constant][0] /* ty=Tensor[(1, 64, 54, 54), float32] */ /* ty=Tensor[(1, 64, 54, 54), float32] */, meta[relay.Constant][0] /* ty=Tensor[(1, 64, 54, 54), float32] */ /* ty=Tensor[(1, 64, 54, 54), float32] */) /* ty=Tensor[(1, 64, 54, 54), float32] */;
      %3 = multiply(%2, 2f /* ty=float32 */) /* ty=Tensor[(1, 64, 54, 54), float32] */;
      %4 = layout_transform(%3, src_layout="NCHW", dst_layout="NCHW16c") /* ty=Tensor[(1, 4, 54, 54, 16), float32] */;
      %5 = add(%1, %4) /* ty=Tensor[(1, 4, 54, 54, 16), float32] */;
      %6 = layout_transform(meta[relay.Constant][0] /* ty=Tensor[(1, 64, 54, 54), float32] */ /* ty=Tensor[(1, 64, 54, 54), float32] */, src_layout="NCHW", dst_layout="NCHW16c") /* ty=Tensor[(1, 4, 54, 54, 16), float32] */;
      %7 = add(%5, %6) /* ty=Tensor[(1, 4, 54, 54, 16), float32] */;
      %8 = add(%5, %6) /* ty=Tensor[(1, 4, 54, 54, 16), float32] */;
      %9 = add(%7, %8) /* ty=Tensor[(1, 4, 54, 54, 16), float32] */;
      layout_transform(%9, src_layout="NCHW16c", dst_layout="NCHW") /* ty=Tensor[(1, 64, 54, 54), float32] */
    }

    // meta data omitted. you can use show_meta_data=True to include meta data



Implement a Pass Using Python Decorator
------------------------------------------
The next example illustrates how we can orchestrate a customized optimization
pipeline through the pass infra using Python decorators. This functionality
greatly eases the implementation of passes. For example, users can simply
define a decorated class to do function-level optimizations as the following
example shows. `transform_function` wraps a class to replace all constants
with a multiple of `c`. Later on, each function in a given module will be
visited and each constant in the function will be replaced when we invoke the
customized pass.


.. code-block:: default


    @relay.transform.function_pass(opt_level=1)
    class CustomPipeline:
        """Simple test function to replace one argument to another."""

        def __init__(self, multiplier):
            self.multiplier = multiplier

        # This function can define a pass.
        def transform_function(self, func, mod, ctx):
            obj = self

            class ReplaceConstant(tvm.relay.ExprMutator):
                def visit_constant(self, c):
                    return relay.multiply(obj.multiplier, c)
            return ReplaceConstant().visit(func)

    f = example()
    mod = tvm.IRModule.from_expr(f)
    custom_pass = CustomPipeline(multiplier=relay.const(3, "float32"))
    assert custom_pass.info.name == "CustomPipeline"
    mod3 = custom_pass(mod)
    print(mod3)





.. rst-class:: sphx-glr-script-out

 Out:

 .. code-block:: none

    def @main(%x: Tensor[(1, 64, 56, 56), float32], %weight: Tensor[(64, 64, 3, 3), float32]) -> Tensor[(1, 64, 54, 54), float32] {
      %0 = nn.conv2d(%x, %weight, padding=[0, 0, 0, 0]) /* ty=Tensor[(1, 64, 54, 54), float32] */;
      %1 = multiply(3f /* ty=float32 */, meta[relay.Constant][0] /* ty=Tensor[(1, 64, 54, 54), float32] */ /* ty=Tensor[(1, 64, 54, 54), float32] */) /* ty=Tensor[(1, 64, 54, 54), float32] */;
      %2 = add(%1, %1) /* ty=Tensor[(1, 64, 54, 54), float32] */;
      %3 = multiply(3f /* ty=float32 */, 2f /* ty=float32 */) /* ty=float32 */;
      %4 = multiply(%2, %3) /* ty=Tensor[(1, 64, 54, 54), float32] */;
      %5 = add(%0, %4) /* ty=Tensor[(1, 64, 54, 54), float32] */;
      %6 = add(%5, %1) /* ty=Tensor[(1, 64, 54, 54), float32] */;
      %7 = add(%5, %1) /* ty=Tensor[(1, 64, 54, 54), float32] */;
      add(%6, %7) /* ty=Tensor[(1, 64, 54, 54), float32] */
    }

    // meta data omitted. you can use show_meta_data=True to include meta data



Debug a Pass
------------
Relay provides users a plug-and-play style debugging pass that print the IR
after a certain pass is done. For example, we can print out the IR on the
completion of constant folding and fusion by adding the debugging pass after
them.


.. code-block:: default


    f = example()
    mod = tvm.IRModule.from_expr(f)
    seq = tvm.transform.Sequential([relay.transform.FoldConstant(),
                                    tvm.transform.PrintIR(),
                                    relay.transform.EliminateCommonSubexpr(),
                                    relay.transform.FuseOps(),
                                    tvm.transform.PrintIR()])
    with tvm.transform.PassContext(opt_level=3):
        mod = seq(mod)

    print("done")




.. rst-class:: sphx-glr-script-out

 Out:

 .. code-block:: none

    done




.. _sphx_glr_download_tutorials_dev_relay_pass_infra.py:


.. only :: html

 .. container:: sphx-glr-footer
    :class: sphx-glr-footer-example



  .. container:: sphx-glr-download

     :download:`Download Python source code: relay_pass_infra.py <relay_pass_infra.py>`



  .. container:: sphx-glr-download

     :download:`Download Jupyter notebook: relay_pass_infra.ipynb <relay_pass_infra.ipynb>`


.. only:: html

 .. rst-class:: sphx-glr-signature

    `Gallery generated by Sphinx-Gallery <https://sphinx-gallery.github.io>`_
